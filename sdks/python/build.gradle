/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * License); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an AS IS BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Define common lifecycle tasks and artifact types
apply plugin: "base"

// For some reason base doesn't define a test task  so we define it below and make
// check depend on it. This makes the Python project similar to the task layout like
// Java projects, see https://docs.gradle.org/4.2.1/userguide/img/javaPluginTasks.png
task test {}
check.dependsOn test

project.ext.envdir = "${project.buildDir}/gradleenv"
subprojects {
  project.ext.envdir = "${project.buildDir}/gradleenv"
}

def tox_opts = "-c tox.ini --recreate"

task setupVirtualenv {
  doLast {
    exec {
      commandLine 'virtualenv', "${envdir}"
    }
    exec {
      executable 'sh'
      args '-c', ". ${envdir}/bin/activate && pip install --upgrade tox==3.0.0 grpcio-tools==1.3.5"
    }
  }
  // Gradle will delete outputs whenever it thinks they are stale. Putting a
  // specific binary here could make gradle delete it while pip will believe
  // the package is fully installed.
  outputs.dirs(project.ext.envdir)
}

configurations { distConfig }

task sdist(dependsOn: 'setupVirtualenv') {
  doLast {
    exec {
      executable 'sh'
      args '-c', ". ${envdir}/bin/activate && python setup.py sdist --formats zip,gztar --dist-dir ${project.buildDir}"
    }
    def collection = fileTree("${project.buildDir}"){ include '**/*.tar.gz' exclude '**/apache-beam.tar.gz'}
    println "sdist archive name: ${collection.singleFile}"
    // we need a fixed name for the artifact
    copy { from collection.singleFile; into "${project.buildDir}"; rename { 'apache-beam.tar.gz' } }
  }
}

artifacts {
  distConfig file: file("${project.buildDir}/apache-beam.tar.gz"), builtBy: sdist
}

task cleanPython(dependsOn: 'setupVirtualenv') {
  doLast {
    exec {
      executable 'sh'
      args '-c', ". ${envdir}/bin/activate && python setup.py clean"
    }
  }
}
clean.dependsOn cleanPython

task buildPython(dependsOn: 'setupVirtualenv') {
  doLast {
    println 'Building Python Dependencies'
    exec {
      executable 'sh'
      args '-c', ". ${envdir}/bin/activate && python setup.py build --build-base ${project.buildDir}"
    }
  }
}
build.dependsOn buildPython

// Snapshot of dependency requirements defined in setup.py.
// Results will be stored in files under Gradle build directory.
task depSnapshot(dependsOn: 'installGcpTest') {
  doLast {
    println 'Snapshoting full dependencies requirements with versions info to requirements.txt.'
    exec {
      // Remove useless item "pkg-resources" from file which is introduced by a bug in Ubuntu.
      executable 'sh'
      args '-c', ". ${envdir}/bin/activate && pip freeze --local --all | grep -v \"pkg-resources\" > ${project.buildDir}/requirements.txt"
    }
  }
}

task lint {}
check.dependsOn lint

task lintPy27(dependsOn: 'setupVirtualenv') {
  doLast {
    exec {
      executable 'sh'
      args '-c', ". ${envdir}/bin/activate && tox ${tox_opts} -e py27-lint"
    }
  }
}
lint.dependsOn lintPy27

task lintPy27_3(dependsOn: 'setupVirtualenv') {
  doLast {
    exec {
      executable 'sh'
      args '-c', ". ${envdir}/bin/activate && tox ${tox_opts} -e py27-lint3"
    }
  }
}
lint.dependsOn lintPy27_3

task lintPy3(dependsOn: 'setupVirtualenv') {
  doLast {
    exec {
      executable 'sh'
      args '-c', ". ${envdir}/bin/activate && tox ${tox_opts} -e py3-lint"
    }
  }
}
lint.dependsOn lintPy3

task testGcp(dependsOn: 'setupVirtualenv') {
  doLast {
    exec {
      executable 'sh'
      args '-c', ". ${envdir}/bin/activate && tox ${tox_opts} -e py27-gcp"
    }
  }
}
test.dependsOn testGcp

task testPython2(dependsOn: 'setupVirtualenv') {
  doLast {
    exec {
      executable 'sh'
      args '-c', ". ${envdir}/bin/activate && tox ${tox_opts} -e py27"
    }
  }
}
test.dependsOn testPython2

task testPython3(dependsOn: 'setupVirtualenv') {
  doLast {
    exec {
      executable 'sh'
      args '-c', ". ${envdir}/bin/activate && tox ${tox_opts} -e py3"
    }
  }
}
test.dependsOn testPython3

task testCython(dependsOn: 'setupVirtualenv') {
  doLast {
    exec {
      executable 'sh'
      args '-c', ". ${envdir}/bin/activate && tox ${tox_opts} -e py27-cython"
    }
  }
}
test.dependsOn testCython
// Ensure that testCython runs exclusively to other tests. This line is not
// actually required, since gradle doesn't do parallel execution within a
// project.
testCython.mustRunAfter testPython2, testGcp

task docs(dependsOn: 'setupVirtualenv') {
  doLast {
    exec {
      executable 'sh'
      args '-c', ". ${envdir}/bin/activate && tox ${tox_opts} -e docs"
    }
  }
}
assemble.dependsOn docs

task cover(dependsOn: 'setupVirtualenv') {
  doLast {
    exec {
      executable 'sh'
      args '-c', ". ${envdir}/bin/activate && tox ${tox_opts} -e cover"
    }
  }
}

task preCommit() {
  dependsOn "docs"
  dependsOn "testCython"
  dependsOn "testPython2"
  dependsOn "testPython3"
  dependsOn "testGcp"
  dependsOn "lint"
}

task installGcpTest(dependsOn: 'setupVirtualenv') {
  doLast {
    exec {
      executable 'sh'
      args '-c', ". ${envdir}/bin/activate && pip install -e .[gcp,test]"
    }
  }
}
installGcpTest.mustRunAfter sdist

def base_test_opts = [
        "--nologcapture",
        "--nocapture",
        "--processes=8",
        "--process-timeout=4000",
]

def convertArgsToString = { argMap ->
  def argList = []
  argMap.each { k, v ->
    if (v in List) {
      v = "\"${v.join(' ')}\""
    }
    argList.add("--$k $v")
  }
}

task directRunnerIT(dependsOn: ['installGcpTest', 'sdist']) {
  def test_opts = base_test_opts + [
          "--attr=IT",
          "--nocapture",
          "--tests=apache_beam.examples.wordcount_it_test:WordCountIT.test_wordcount_it," +
                  "apache_beam.io.gcp.pubsub_integration_test:PubSubIntegrationTest",
  ]
  def argMap = [
          "runner": "TestDirectRunner",
          "test_opt": test_opts,
  ]
  // Run tests with TestDirectRunner in batch.
  def batchCmdArgs = convertArgsToString argMap
  doLast {
    exec {
      executable 'sh'
      args '-c', ". ${envdir}/bin/activate && ./scripts/run_integration_test.sh $batchCmdArgs"
    }
  }
  // Run tests with TestDirectRunner in streaming.
  argMap["streaming"] = "true"
  def streamingCmdArgs = convertArgsToString argMap
  doLast {
    exec {
      executable 'sh'
      args '-c', ". ${envdir}/bin/activate && ./scripts/run_integration_test.sh $streamingCmdArgs"
    }
  }
}

// Before running this, you need to:
//
// 1. Build the SDK container:
//
//    ./gradlew -p sdks/python/container docker
//
// 2. Either a) or b)
//  a) If you want the Job Server to run in a Docker container:
//
//    ./gradlew :beam-runners-flink_2.11-job-server-container:docker
//
//  b) Otherwise, start a local JobService, for example, the Portable Flink runner
//    (in a separate shell since it continues to run):
//
//    ./gradlew :beam-runners-flink_2.11-job-server:runShadow
//
// Then you can run this example:
//
//  Docker (2a):
//
//    ./gradlew :beam-sdks-python:portableWordCount
//
//  Local JobService (2b):
//
//    ./gradlew :beam-sdks-python:portableWordCount -PjobEndpoint=localhost:8099
//
task portableWordCount(dependsOn: 'installGcpTest') {
  doLast {
    // TODO: Figure out GCS credentials and use real GCS input and output.
    def options = [
            "--input=/etc/profile",
            "--output=/tmp/py-wordcount-direct",
            "--runner=PortableRunner",
    ]
    if (project.hasProperty("streaming"))
      options += ["--streaming"]
    if (project.hasProperty("jobEndpoint"))
      options += ["--job_endpoint=${project.property('jobEndpoint')}"]
    exec {
      executable 'sh'
      args '-c', ". ${envdir}/bin/activate && python -m apache_beam.examples.wordcount ${options.join(' ')}"
      // TODO: Check that the output file is generated and runs.
    }
  }
}

task integrationTest(dependsOn: ['installGcpTest', 'sdist']) {
  doLast {
    def argMap = [:]

    // Build test options that configures test environment and framework
    def test_options = base_test_opts
    if (project.hasProperty('testOptions'))
      // Customize test options by adding more nose flags to -Ptest_opt in commandline
      test_options = project.property('test_options')
    if (project.hasProperty('attr'))
      test_options += ["--attr=${project.property('attr')}"]
    if (project.hasProperty('tests'))
      test_options += ["--tests=${project.property('tests')}"]

    argMap["test_opts"] = test_options

    // Build pipeline options that configures pipeline job
    if (project.hasProperty('pipelineOptions'))
      argMap["pipeline_opts"] = project.property('pipelineOptions')

    def cmdArgs = mapArgToString argMap
    exec {
      executable 'sh'
      args '-c', ". ${envdir}/bin/activate && ./scripts/run_integration_test.sh $cmdArgs"
    }
  }
}

//task postCommitITTests(dependsOn: ['installGcpTest', 'sdist']) {
//  doLast {
//    def test_opts = base_test_opts + ["--attr=IT"]
//    exec {
//      executable 'sh'
//      args '-c', ". ${envdir}/bin/activate && ./scripts/run_integration_test.sh"
//    }
//  }
//}

task validatesRunnerBatchTests(dependsOn: 'installGcpTest') {
  doLast {
    exec {
      executable 'sh'
      args '-c', ". ${envdir}/bin/activate && ./scripts/run_integration_test.sh --test_opts --attr=ValidatesRunner"
    }
  }
}

task validatesRunnerStreamingTests(dependsOn: 'installGcpTest') {
  doLast {
    exec {
      executable 'sh'
      // TODO(BEAM-3544,BEAM-5025): Disable tests with sickbay-streaming tag.
      args '-c', ". ${envdir}/bin/activate && ./scripts/run_integration_test.sh --attr ValidatesRunner,'!sickbay-streaming' --streaming true"
    }
  }
}

task hdfsIntegrationTest(dependsOn: 'installGcpTest') {
  doLast {
    exec {
      executable 'sh'
      args '-c', ". ${envdir}/bin/activate && ./apache_beam/io/hdfs_integration_test/hdfs_integration_test.sh"
    }
  }
}

class CompatibilityMatrixConfig {
  // Execute batch or streaming pipelines.
  boolean streaming = false
  // Execute on Docker or Process based environment.
  SDK_WORKER_TYPE workerType = SDK_WORKER_TYPE.DOCKER

  enum SDK_WORKER_TYPE {
    DOCKER, PROCESS
  }
}

def flinkCompatibilityMatrix = {
  def config = it ? it as CompatibilityMatrixConfig : new CompatibilityMatrixConfig()
  def workerType = config.workerType.name()
  def streaming = config.streaming
  def environment_config = config.workerType == CompatibilityMatrixConfig.SDK_WORKER_TYPE.PROCESS ? "--environment_config='{\"command\": \"${project(":beam-sdks-python:").buildDir.absolutePath}/sdk_worker.sh\"}'" : ""
  def name = "flinkCompatibilityMatrix${streaming ? 'Streaming' : 'Batch'}${workerType}"
  tasks.create(name: name) {
    dependsOn 'setupVirtualenv'
    dependsOn ':beam-runners-flink_2.11-job-server:shadowJar'
    if (workerType.toLowerCase() == 'docker')
      dependsOn ':beam-sdks-python-container:docker'
    else
      dependsOn 'createProcessWorker'
    doLast {
      exec {
        executable 'sh'
        args '-c', ". ${envdir}/bin/activate && pip install -e . && python -m apache_beam.runners.portability.flink_runner_test --flink_job_server_jar=${project(":beam-runners-flink_2.11-job-server:").shadowJar.archivePath} --environment_type=${workerType} ${environment_config} ${streaming ? '--streaming' : ''}"
      }
    }
  }
}

task flinkCompatibilityMatrixDocker() {
  dependsOn flinkCompatibilityMatrix(streaming: false)
  dependsOn flinkCompatibilityMatrix(streaming: true)
}

task flinkCompatibilityMatrixProcess() {
  dependsOn flinkCompatibilityMatrix(streaming: false, workerType: CompatibilityMatrixConfig.SDK_WORKER_TYPE.PROCESS)
  dependsOn flinkCompatibilityMatrix(streaming: true, workerType: CompatibilityMatrixConfig.SDK_WORKER_TYPE.PROCESS)
}

task flinkValidatesRunner() {
  dependsOn 'flinkCompatibilityMatrixDocker'
}

task postCommit() {
  dependsOn "preCommit"
  dependsOn "directRunnerIT"
  dependsOn "hdfsIntegrationTest"
  dependsOn ":postcommit:postCommitIT"
}

task dependencyUpdates(dependsOn: ':dependencyUpdates') {
  doLast {
    exec {
      executable 'sh'
      args '-c', "./scripts/run_dependency_check.sh"
    }
  }
}

task buildSnapshot() {
  dependsOn 'sdist'
  dependsOn 'depSnapshot'
}

project.task('createProcessWorker') {
  dependsOn ':beam-sdks-python-container:build'
  dependsOn 'setupVirtualenv'
  def sdkWorkerFile = file("${project.buildDir}/sdk_worker.sh")
  def workerScript = "${project(":beam-sdks-python-container:").buildDir.absolutePath}/target/launcher/linux_amd64/boot"
  def sdkWorkerFileCode = "sh -c \". ${envdir}/bin/activate && ${workerScript} \$* \""
  outputs.file sdkWorkerFile
  doLast {
    sdkWorkerFile.write sdkWorkerFileCode
    exec {
      commandLine('sh', '-c', ". ${envdir}/bin/activate && cd ${project.projectDir} && python setup.py install ")
    }
    exec {
      commandLine('chmod', '+x', sdkWorkerFile)
    }
  }
}
